#============================================================
#    Conversation
#============================================================
# - author: zhangxuetu
# - datetime: 2025-01-06 14:49:17
# - version: 4.3.0.stable
#============================================================
## Chat èŠå¤©çš„å¯¹è¯
##
##å¤„ç†å¯¹è¯å†…å®¹ï¼Œå¹¶å¤„ç†æ•°æ®
class_name Conversation
extends Node


signal requested(message_data: Dictionary) ##å·²å‘å‡ºè¯·æ±‚
signal responded_message(message_data: Dictionary) ##å·²å“åº”è¯·æ±‚ç»“æœ
signal responded_stream_data(delta_data: Dictionary) ##æ¯å¸§çš„æµæ•°æ®
signal responded_stream_end(message_data: Dictionary) ##æ•°æ®æµç»“æŸ
signal responded_error(error_data: Dictionary)
signal saved ##å·²ä¿å­˜


const Role = {
	SYSTEM = "system",
	ASSISTANT = "assistant",
	USER = "user",
}
const ResponseFormat = {
	TEXT = "text",
	JSON_OBJECT = "json_object",
}

@export var base_url : String = ""
@export var model: String = ""
@export var api_key: String = ""

@export var saved_propertys : PackedStringArray = []

@export var stream: bool = false ##æ˜¯å¦å¼€å¯æµè¾“å‡º
@export var tool_mode : bool = false ## å¦‚æœä¸ºå·¥å…·æ¨¡å¼ï¼Œåˆ™æ²¡æœ‰ä¸Šä¸‹æ–‡ï¼Œåªæœ‰ç¬¬ä¸€ä¸ªæ¶ˆæ¯å’Œæœ€åä¸€ä¸ªæ¶ˆæ¯ä¼šè¢«å‘é€
@export_multiline var tool_message: String ##å·¥å…·æ¨¡å¼çš„æç¤ºè¯
@export var messages: Array[Dictionary] = []  ##æ‰€æœ‰æ¶ˆæ¯
@export_global_dir var file_path: String

var _http_request: HTTPRequest
var _stream_request: StreamRequest
var _is_running: bool = false
var _delta_datas : Dictionary = {}


func _init() -> void:
	_http_request = HTTPRequest.new()
	_http_request.request_completed.connect(_request_completed)
	
	_stream_request = StreamRequest.new()
	_stream_request.responded.connect(_response_stream_data)
	_stream_request.connect_closed.connect(_response_stream_end)
	_stream_request.responded_error.connect(
		func(status):
			_response_stream_end()
			HTTPClient.STATUS_BODY
	)
	
	# æ·»åŠ httpè¯·æ±‚èŠ‚ç‚¹
	var root: SceneTree = Engine.get_main_loop()
	if root.current_scene:
		root.current_scene.add_child.call_deferred(_http_request)
		root.current_scene.add_child.call_deferred(_stream_request)
	else:
		root.process_frame.connect(
			func(): 
				if is_instance_valid(self):
					root.current_scene.add_child(_http_request)
					root.current_scene.add_child(_stream_request)
				,
			Object.CONNECT_ONE_SHOT
		)


func _notification(what: int) -> void:
	if what == NOTIFICATION_PREDELETE:
		if is_instance_valid(_stream_request):
			_stream_request.queue_free()
			_http_request.queue_free()


## å‘é€è¯·æ±‚
func send(message: String) -> void:
	if is_running():
		push_error("æ­£åœ¨è¿è¡Œä¸­ï¼Œåœ¨æ­¤æœŸé—´ä¸èƒ½å‘é€")
		return
	
	_delta_datas = {}
	message = message.strip_edges()
	assert(message != "", "æ¶ˆæ¯æ•°æ®ä¸èƒ½ä¸ºç©º")
	while not _http_request or not _http_request.is_inside_tree():
		await Engine.get_main_loop().process_frame
	
	# å½“å‰æ•°æ®
	var message_data : Dictionary = {
		"role": Role.USER,
		"content": message,
	}
	messages.push_back(message_data)
	
	# æ•°æ®åˆ—è¡¨
	var temp_message : Array = []
	if tool_mode:
		# å·¥å…·æ¨¡å¼.æ— ä¸Šä¸‹æ–‡
		temp_message.push_back({
			"role": Role.SYSTEM,
			"content": tool_message,
		})
		temp_message.push_back(message_data)
	else:
		# å¸¦æœ‰ä¸Šä¸‹æ–‡
		var last_role = ""
		for item in messages:
			if last_role != item["role"]:
				last_role = item["role"]
				temp_message.push_back({
					"role": item["role"],
					"content": item["content"],
				})
	
	# å‘å‡ºæ—¶çš„æ•°æ®
	var body : Dictionary = {
		"messages": temp_message,
		"model": model,
		"stream": stream,
	}
	requested.emit(message_data)
	
	# å¼€å§‹æ­£å¼è¯·æ±‚æ•°æ®
	var body_json : String = JSON.stringify(body)
	var headers : Array = [
		"Content-Type: application/json",
		"Authorization: Bearer " + api_key,
	]
	if stream:
		_stream_request.close()
		_stream_request.request(base_url, headers, HTTPClient.METHOD_POST, body_json)
	else:
		_http_request.request(base_url, headers, HTTPClient.METHOD_POST, body_json)


## åœæ­¢å“åº”
func stop() -> void:
	if stream:
		if _stream_request.is_connected:
			_stream_request.close()
	else:
		if _http_request.get_http_client_status() == HTTPClient.STATUS_REQUESTING:
			_http_request.cancel_request()


## æ˜¯å¦æ­£åœ¨è¿è¡Œ
func is_running() -> bool:
	if stream:
		return _stream_request.is_connected
	else:
		return _http_request.get_http_client_status() == HTTPClient.STATUS_REQUESTING


## ä¿å­˜ä¼šè¯èµ„æºã€‚ä¸ä¼ å…¥ path å‚æ•°ï¼Œåˆ™é»˜è®¤æŒ‰ç…§å½“å‰ file_path çš„è·¯å¾„è¿›è¡Œä¿å­˜ 
func save(path: String = "") -> String:
	assert(path != "" or file_path != "", "æ–‡ä»¶åä¸èƒ½ä¸ºç©º")
	if path != "":
		self.file_path = path
	
	# å¯¼å‡ºçš„æ•°æ®
	if saved_propertys.is_empty():
		const EXPORT_USAGE = PROPERTY_USAGE_SCRIPT_VARIABLE | PROPERTY_USAGE_DEFAULT
		var property : String
		for item in get_script().get_script_property_list():
			if item['usage'] & EXPORT_USAGE == EXPORT_USAGE:
				property = item["name"]
				saved_propertys.push_back(property)
	
	# å±æ€§æ•°æ®
	var data : Dictionary = {}
	for property in saved_propertys:
		data[property] = get(property)
	
	# ä¿å­˜
	FileUtil.write_as_var(file_path, data)
	saved.emit()
	return file_path


## åŠ è½½ä¼šè¯æ–‡ä»¶
static func load(path: String) -> Conversation:
	var res: Conversation
	res = Conversation.new()
	res.file_path = path
	if FileUtil.file_exists(path):
		# è®¾ç½®å±æ€§æ•°æ®
		var data : Dictionary = FileUtil.read_as_var(path)
		for property in data:
			res.set(property, data[property])
	return res


func _handle_data(data: Dictionary) -> Dictionary:
	if data:
		var choices : Dictionary = data["choices"][0]
		if choices.has("message"):
			return choices["message"]
		elif choices.has("delta"):
			return choices["delta"]
	return {}


# #å“åº”ç»“æœç»“æ„ï¼šhttps://api-docs.deepseek.com/zh-cn/api/create-chat-completion
#{
#  "id": "30230b91-db94-4e74-bd24-8ca4ab13b4fa",
#  "object": "chat.completion",
#  "created": 1736317458,
#  "model": "deepseek-chat",
#  "choices": [
#    {
#      "index": 0,
#      "message": {
#        "role": "assistant",
#        "content": "Hello! How can I assist you today? ğŸ˜Š"
#      },
#      "logprobs": null,
#      "finish_reason": "stop"
#    }
#  ],
#  "usage": {
#    "prompt_tokens": 9,
#    "completion_tokens": 11,
#    "total_tokens": 20,
#    "prompt_cache_hit_tokens": 0,
#    "prompt_cache_miss_tokens": 9
#  },
#  "system_fingerprint": "fp_3a5770e1b4"
#}
func _request_completed(result: int, response_code: int, headers: PackedStringArray, body: PackedByteArray) -> void:
	var v : String = body.get_string_from_utf8()
	var json : JSON = JSON.new()
	if json.parse(v) == OK:
		var data = json.data
		if data.has("error"):
			responded_error.emit(data["error"])
		else:
			var message : Dictionary = _handle_data(data)
			messages.push_back(message)
			responded_message.emit(message)


#{
#	"choices": [
#		{
#			"delta": {
#				"content": "",
#				"reasoning_content": "",
#				"role": "assistant"
#			},
#			"finish_reason": null,
#			"index": 0.0,
#			"logprobs": null
#		}
#	],
#	"created": 1736317939.0,
#	"id": "3e454c66-8805-4563-a807-e7e2aebfdece",
#	"model": "deepseek-chat",
#	"object": "chat.completion.chunk",
#	"system_fingerprint": "fp_3a5770e1b4"
#}
func _response_stream_data(body_chunk: PackedByteArray):
	var text : String = body_chunk.get_string_from_utf8().strip_edges()
	var items : Array = text.split("data: ")
	#for i in items:
		#print(i)
	#print_debug()
	#print()
	var json : JSON = JSON.new()
	for item in items:
		if item != "" and not text.ends_with("[DONE]"):
			if json.parse(item) == OK:
				var data : Dictionary = json.data
				if data.has("choices"):
					var delta = _handle_data(data)
					if _delta_datas.is_empty():
						_delta_datas["id"] = data["id"]
						_delta_datas["role"] = delta["role"]
						_delta_datas["content"] = ""
						_delta_datas["reasoning_content"] = ""
					if delta.has("content") and typeof(delta["content"]) != TYPE_NIL and delta["content"] != "":
						_delta_datas["content"] += delta["content"]
					elif delta.has("reasoning_content") and typeof(delta["reasoning_content"]) != TYPE_NIL and delta["reasoning_content"] != "":
						_delta_datas["reasoning_content"] += delta["reasoning_content"]
					#else:
						#continue
					responded_stream_data.emit(_delta_datas)
				else:
					responded_error.emit(data["error"])
			else:
				if str(item).contains(": keep-alive"):
					print_debug("ä¿æŒç›‘å¬ä¸­ï¼Œè¯·ç­‰å¾…...")


func _response_stream_end():
	if not _delta_datas.is_empty():
		var message_data = {
			"role": _delta_datas["role"],
			"content": _delta_datas["content"],
		}
		if _delta_datas.has("reasoning_content") and _delta_datas["reasoning_content"]:
			message_data["reasoning_content"] = _delta_datas["reasoning_content"]
		messages.push_back(message_data)
	else:
		_delta_datas["role"] = ""
		_delta_datas["content"] = ""
		_delta_datas["reasoning_content"] = ""
	responded_stream_end.emit(_delta_datas)
	_delta_datas = {}
